#!/bin/zsh

# Upload one or more files to https://file.io. Also yanks the url(s) to
# clipboard if you have `xclip` installed.

# Dependencies: jq

# Optional Dependencies: xclip

TTL="1d" # Files live for 1 day by default
BASE_URL="https://file.io"

usage() {
cat <<EOF
Usage: fileio [OPTIONS] [FILES]

Options:

-t      Specify TTL of file in [d]ays, [w]eeks, [m]onths, or [y]ears.

Examples:

fileio -t 2d file.txt file-2.txt

fileio -t 1y file.txt
EOF

exit 1
}

while getopts ":t:h" opt; do
  case $opt in
    t)
      TTL=$OPTARG
      ;;
    h)
      usage
      ;;
    \?)
      echo "Invalid option: -$OPTARG" >&2
      ;;
    :)
      echo "Option -$OPTARG requires an argument" >&2
      usage
      ;;
  esac
done

# Get all files after options
files=(${@:$OPTIND})
file_count=${#files[@]}

# Print usage & quit if no files were specified
[ $file_count -eq 0 ] && usage

links=""

# Upload a file to file.io, and append resulting url to $urls
function upload() {
    response=$(curl -s -F "file=@$1" "$BASE_URL/?expires=$TTL")
    link=$(echo $response | jq '.["link"]' | cut -d '"' -f2)
    error=$(echo $response | jq '.["error"]')

    if [[ "$link" == "null" ]]; then
        echo $error
        exit 1
    else
        if [ -z $links ]; then
            links=$link
        else
            links="$links\n$link"
        fi
    fi
}

# Upload each input file
for file in $files; do
    upload $file
done

# Copy url to clipboard if xclip is installed
hash xclip 2> /dev/null && echo $links | xclip -selection c

echo $links
